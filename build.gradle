/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/3.4.1/userguide/tutorial_java_projects.html
 */

apply plugin: 'java'            // Add Java support
apply plugin: 'application'     // Add support for packaging and running applications
apply plugin: 'jdepend'         // Add JDepend integration
apply plugin: 'checkstyle'      // Add Checkstyle integration
apply plugin: 'idea'            // Add IntelliJ IDEA support

version = 0.1
sourceCompatibility = 1.8
mainClassName = 'com.sterilecode.mitosis.Main'

def versions = [
        checkstyle: '7.6.1',
        junit: '4.12'
]

repositories {
    jcenter()
}

configurations {
    checkstyleConfig
}

dependencies {
    testCompile "junit:junit:${versions.junit}"
    checkstyleConfig ("com.puppycrawl.tools:checkstyle:${versions.checkstyle}") {
        transitive = false
    }
}

tasks.withType(JDepend) {
    reports {
        text.enabled = true
        xml.enabled = false
    }
}

checkstyle {
    toolVersion = "${versions.checkstyle}"
    config = resources.text.fromArchiveEntry(configurations.checkstyleConfig, 'google_checks.xml')
}


